plugins {
    id('com.github.johnrengelman.shadow') version('8.1.1')
}

dependencies {
    // YAML
    implementation("dev.dejvokep:boosted-yaml:1.3")

    // LuckPerms
    compileOnly("net.luckperms:api:5.4")

    // JDA
    implementation("net.dv8tion:JDA:5.0.0-beta.10") {
        exclude(module: "opus-java")
    }

    // Websocket
    implementation("com.neovisionaries:nv-websocket-client:2.14")

    // Logging
    compileOnly("org.slf4j:slf4j-api:1.7.36")
}

shadowJar {
    dependencies {
        exclude(dependency("org.slf4j:slf4j-api:1.7.36"))
    }

    // YAML
    relocate("dev.dejvokep", "ca.sperrer.p0t4t0sandwich.tatercomms.lib.dejvokep")

    // JDA
    relocate("net.dv8tion", "ca.sperrer.p0t4t0sandwich.tatercomms.lib.dv8tion")
    relocate("org.apache", "ca.sperrer.p0t4t0sandwich.tatercomms.lib.apache")
    relocate("okhttp3", "ca.sperrer.p0t4t0sandwich.tatercomms.lib.okhttp3")
    relocate("com.iwebpp", "ca.sperrer.p0t4t0sandwich.tatercomms.lib.iwebpp")
    relocate("kotlin", "ca.sperrer.p0t4t0sandwich.tatercomms.lib.kotlin")
    relocate("com.fasterxml.jackson", "ca.sperrer.p0t4t0sandwich.tatercomms.lib.jackson")
    relocate("gnu.trove", "ca.sperrer.p0t4t0sandwich.tatercomms.lib.trove")
    relocate("okio", "ca.sperrer.p0t4t0sandwich.tatercomms.lib.okio")

    // Websocket
    relocate("com.neovisionaries", "ca.sperrer.p0t4t0sandwich.tatercomms.lib.neovisionaries")

    destinationDirectory = file('../target')
    archiveFileName = "TaterComms-${version}-common.jar"
}
shadowJar.doLast {
    task ->
        ant.checksum(file: task.archivePath) {
            hashAlgorithm = "md5"
            property = "md5sum"
        }
}

artifacts {
    archives(shadowJar)
}

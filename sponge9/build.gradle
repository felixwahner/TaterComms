import org.spongepowered.gradle.plugin.config.PluginLoaders
import org.spongepowered.plugin.metadata.model.PluginDependency

plugins {
    id('com.github.johnrengelman.shadow') version('8.1.1')
    id('java-library')
    id("org.spongepowered.gradle.plugin") version('2.1.1')
}

targetCompatibility = java_version

sponge {
    apiVersion(spongeapi_version)
    license("GPL-3.0")
    loader {
        name(PluginLoaders.JAVA_PLAIN)
        version("1.0")
    }
    plugin(project_id) {
        version("1.18-${version}")
        displayName(project_name)
        entrypoint("dev.neuralnexus.tatercomms.sponge.SpongeTaterCommsPlugin")
        description("A simple, cross API plugin that bridges communication between servers, using built-in Proxy methods, Discord channels and TCP sockets.")
        links {
             homepage("https://github.com/p0t4t0sandwich/TaterComms#readme")
             source("https://github.com/p0t4t0sandwich/TaterComms")
             issues("https://github.com/p0t4t0sandwich/TaterComms/issues")
        }
        dependency("spongeapi") {
            loadOrder(PluginDependency.LoadOrder.AFTER)
            optional(false)
        }
        dependency("taterlib") {
            version("1.0.2")
            loadOrder(PluginDependency.LoadOrder.AFTER)
            optional(false)
        }
        dependency("luckperms") {
            version("5.3.0")
            loadOrder(PluginDependency.LoadOrder.AFTER)
            optional(true)
        }
    }
}

dependencies {
    implementation(project(path: ':common', configuration: 'shadow'))

    // TaterLib
    compileOnly("dev.neuralnexus:TaterLib:1.16-${taterlib_version}")
}

shadowJar {
    dependencies {
        include(project(":common"))
    }

    destinationDirectory = file('../target')
    archiveFileName = "${project_name}-sponge9-${version}.jar"
}
shadowJar.doLast {
    task ->
        ant.checksum(file: task.archivePath) {
            hashAlgorithm = 'md5'
            property = 'md5sum'
        }
}

artifacts {
    archives(shadowJar)
}
